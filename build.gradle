apply plugin: 'java'

group 'org.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}


configurations {
    jaxb
}

dependencies {
    // Internal
    compile 'org.ringingmaster:ringingmaster-persist:1.0.0'
    compile 'org.ringingmaster:ringingmaster-engine:1.0.0'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.2.11'
    jaxb 'org.glassfish.jaxb:jaxb-runtime:2.2.11'

//    // TEST: General
    testCompile 'junit:junit:4.12'
//
//    // TEST: Logging
    testRuntime 'org.apache.logging.log4j:log4j-core:2.8.2'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
}

def generatedCCBRMethodLibraryModelSchemaDir = "${projectDir}/src/main/ccResources"
def generatedCCBRMethodLibraryModelClassesDir = "${buildDir}/classes/generated/jaxb"
def generatedCCBRMethodLibraryModelGeneratedDir = "${projectDir}/src/generated/java"

// Add generated code into source set.
sourceSets.main.java.srcDir generatedCCBRMethodLibraryModelGeneratedDir
sourceSets.main.resources.srcDir generatedCCBRMethodLibraryModelSchemaDir

task generateCCBRMethodLibraryJaxbJavaFiles (group: 'ringingmaster') {

    // Drive the UP-TO-DATE mechanism
    inputs.dir generatedCCBRMethodLibraryModelSchemaDir
    outputs.dir generatedCCBRMethodLibraryModelClassesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: generatedCCBRMethodLibraryModelGeneratedDir)
            mkdir(dir: generatedCCBRMethodLibraryModelClassesDir)

            xjc(destdir: generatedCCBRMethodLibraryModelGeneratedDir) {
                schema(dir: generatedCCBRMethodLibraryModelSchemaDir, includes: "**/*.xsd")
                arg(value: "-nv")
                produces(dir: generatedCCBRMethodLibraryModelGeneratedDir, includes: "**/*.java")
            }
        }
    }
}

// Ensure the generated files are in the compiled source, along with forcing the generation process
compileJava.source generateCCBRMethodLibraryJaxbJavaFiles.outputs.files, sourceSets.main.java

clean{
    delete generatedCCBRMethodLibraryModelGeneratedDir
}


def artifactsDir = "${buildDir}/artifacts"

task generateMethodLibrary (group: 'ringingmaster', dependsOn: ['generateCCBRMethodLibraryJaxbJavaFiles', 'classes'], type: JavaExec) {
    project.ant.mkdir(dir: artifactsDir)
    
    main = 'com.ringingmaster.extract.NotationLibraryBuilder'
    classpath(sourceSets.main.runtimeClasspath)
    classpath(sourceSets.test.runtimeClasspath) //To get the logging libs
    args '/allmeths.xml', "${artifactsDir}/notationlibrary.xml"
}
configurations {
    jaxb
}

dependencies {
    compile project(':ringingmaster-persist')

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}


sourceSets {
    main {
        java {
            srcDir 'src/generated/java'
        }
        resources {
            srcDir 'src/main/ccResources'
        }
    }
}



task generateCCBRMethodLibraryJaxbJavaFiles (dependsOn: 'clean') {
    ext.sourcesDir = "${projectDir}/src/generated/java"
    ext.classesDir = "${buildDir}/classes/jaxb"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir) {
                schema(dir: "${projectDir}/src/main/ccResources", includes: "**/*.xsd")
                arg(value: "-nv")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}


task generateMethodLibrary (group: 'ringingmaster', dependsOn: ['generateCCBRMethodLibraryJaxbJavaFiles', 'classes'], type: JavaExec) {
    main = 'com.ringingmaster.extraction.NotationLibraryBuilder'
    classpath = sourceSets.main.runtimeClasspath
    args '/allmeths.xml', project.getBuildDir().toString() + '/notationlibrary.xml'
}